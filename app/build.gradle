apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "io.github.tjg1.nori"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 4
        versionName "2.2.0"
        testApplicationId "io.github.tjg1.nori.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        buildConfigField "boolean", "DONATIONS_PAYPAL", "true"
        buildConfigField "boolean", "DONATIONS_PATREON", "true"
        buildConfigField "boolean", "DONATIONS_BITCOIN", "true"
        buildConfigField "boolean", "DONATIONS_GOOGLE", "false"

        buildConfigField "String", "DONATIONS_PAYPAL_EMAIL", "\"tomg@fastmail.uk\""
        buildConfigField "String", "DONATIONS_PATREON_ACCOUNT", "\"3696048\""
        buildConfigField "String", "DONATIONS_BITCOIN_ADDRESS", "\"1NHUsSzj3N7TE8rykUYk8rqqLWWSmTVzdp\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-${gitRevision()}"
        }
    }
    compileOptions {
        // Enable Java 1.7 features.
        sourceCompatibility JavaVersion.VERSION_1_7;
        targetCompatibility JavaVersion.VERSION_1_7;
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:support-v4:24.1.1'

    // Third-party libraries.
    // An HTTP+SPDY client for Android and Java applications. http://square.github.io/okhttp/
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.5'
    // A powerful image downloading and caching library for Android http://square.github.io/picasso/
    compile 'com.squareup.picasso:picasso:2.5.2'
    // ImageView with Pan & Zoom support for viewing images
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.2.5'
    // Utility library for easier unit testing.
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'

    // Project dependencies.
    compile project(':norilib') // https://github.com/tjg1/norilib

    // *.jar files.
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

/*
 * This file is part of nori.
 * Copyright (c) 2014-2016 Tomasz Jan GÃ³ralczyk <tomg@fastmail.uk>
 * License: GNU GPLv2
 */

// Used to append Git commit hash to version names.
def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}
